# Maintainer: Chloe Pelling <cpelling@google.com>

pkgname=lib32-cros-wayland
pkgver=1.21.0
pkgrel=1
pkgdesc='wayland library (32-bit)'
arch=(x86_64)
url=https://wayland.freedesktop.org
license=(MIT)
depends=(
  lib32-glibc
  lib32-libffi
  lib32-expat
  lib32-libxml2
  cros-wayland
)
makedepends=(meson)
conflicts=('lib32-wayland')
provides=('lib32-wayland' libwayland-{client,cursor,egl,server}.so)
validpgpkeys=('C7223EBE4EF66513B892598911A30156E0E67611'  # Bryce Harrington
              'C0066D7DB8E9AC6844D728715E54498E697F11D7'  # Derek Foreman
              '34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48') # emersion <contact@emersion.fr>
_borealis_branch_tracked=('borealis')
_borealis_current_ref=('commit=8230237744099f1c6c3229de2ce8720dd413c933')
source=("wayland::git+https://chromium.googlesource.com/chromiumos/third_party/wayland#${_borealis_current_ref}")
sha256sums=('SKIP')
options=(debug strip)

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  cd ${srcdir}/wayland
  meson build \
    --buildtype=release \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    -Ddocumentation="false" \
    -Ddtd_validation="false" \
    -Ddebug_util="false"
  ninja -C build
}

package() {
  cd ${srcdir}/wayland
  DESTDIR="$pkgdir" ninja -C build install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s cros-wayland "${pkgdir}"/usr/share/licenses/"${pkgname}"
}
